import java.text.SimpleDateFormat

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def libraryVersion = '3.0.5'
def libraryVersionCode = 30005

// Maven group and version
version = libraryVersion
def siteUrl = 'https://github.com/Wakup/Wakup-Android-SDK'      // Homepage URL of the library
def gitUrl = 'https://github.com/Wakup/Wakup-Android-SDK.git'   // Git repository URL
group = "com.wakup.android"                                     // Maven Group ID for the artifact


android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    resourcePrefix 'wk_'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionName libraryVersion
        versionCode libraryVersionCode

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Carousel view
    // Tags Flow Layout
    compile 'com.google.android.gms:play-services-maps:17.0.0'
    compile 'com.google.android.gms:play-services-location:17.0.0'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'me.grantland:autofittextview:0.2.1'
    compile 'com.android.volley:volley:1.1.1'
    compile 'androidx.appcompat:appcompat:1.1.0'
    compile 'com.google.android.material:material:1.0.0'
    compile 'androidx.cardview:cardview:1.0.0'
    compile 'androidx.recyclerview:recyclerview:1.0.0'
    compile 'org.apmem.tools:layouts:1.10@aar'
    compile 'androidx.constraintlayout:constraintlayout:1.1.3'
    compile 'com.wefika:horizontal-picker:1.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Libraries used in client application
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.synnapps:carouselview:0.0.10'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Avoid Kotlin docs error
tasks.withType(Javadoc) {
    enabled = false
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "wakup"
        repo = "sdk"
        name = "android-sdk"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true

        version {
            name = libraryVersion
            desc = 'Wakup SDK for Android ' + libraryVersion
            released = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date())
            vcsTag = 'v' + libraryVersion
        }
    }
}
repositories {
    mavenCentral()
}