import java.text.SimpleDateFormat

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def libraryVersion = '2.1.5'
def libraryVersionCode = 20105

// Maven group and version
version = libraryVersion
def siteUrl = 'https://github.com/Wakup/Wakup-Android-SDK'      // Homepage URL of the library
def gitUrl = 'https://github.com/Wakup/Wakup-Android-SDK.git'   // Git repository URL
group = "com.wakup.android"                                     // Maven Group ID for the artifact


android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'

    resourcePrefix 'wk_'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionName libraryVersion
        versionCode libraryVersionCode

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Carousel view
    // Tags Flow Layout
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'me.grantland:autofittextview:0.2.1'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.synnapps:carouselview:0.0.10'
    implementation 'org.apmem.tools:layouts:1.10@aar'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.wefika:horizontal-picker:1.1.1'
    // Libraries used in client application
    api 'uk.co.chrisjenx:calligraphy:2.2.0'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = "wakup"
        repo = "sdk"
        name = "android-sdk"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true

        version {
            name = libraryVersion
            desc = 'Wakup SDK for Android ' + libraryVersion
            released = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZZ").format(new Date())
            vcsTag = 'v' + libraryVersion
        }
    }
}